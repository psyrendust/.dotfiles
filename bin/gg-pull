#!/usr/bin/env zsh
#
# ggpull
# Update the current branch. Optionally update a different branch without
# switching to it.
#-------------------------------------------------------------------------------

set -e

BRANCH_FLAG=("-b" "--branch")
UPDATE_FLAG=("-u" "--update" "-r" "--refresh")
ALL_FLAGS=($BRANCH_FLAG $UPDATE_FLAG)
targetBranch=""
destinationBranch="$(git-current-branch)";

# Check if an arg has been passed and it matches one of our flags
if (( $ALL_FLAGS[(Ie)$1] )); then
  # Get the target branch from our local branches using fzf
  targetBranch=$(getgitbranch --header="Select a target branch to rebase onto ${destinationBranch}")
  if [[ -z "$targetBranch" ]]; then
    exit;
  fi

  # Update a different branch without switching to it
  chalk -t "[ {blue GGPULL} ] updating branch {cyan $targetBranch}"
  git fetch origin ${targetBranch}:${targetBranch}

  if (( $UPDATE_FLAG[(Ie)$1] )); then
    chalk -t "[ {blue GGPULL} ] rebasing {green.bold $targetBranch} onto {blue.bold $destinationBranch}"
    # Rebase changes from targetBranch onto the current branch
    git rebase $targetBranch
  fi
else
  # Use the current branch as the target branch
  if [[ -z "$destinationBranch" ]]; then
    chalk -t "[ {red GGPULL} ] {red branch not found} {cyan $destinationBranch}"
    exit;
  fi

  git pull origin ${destinationBranch}
fi
